.org $5200

init:
    lda #22         ; MODE 5
    jsr $ffee
    lda #5
    jsr $ffee

    lda #23         ; disable flashing cursor
    jsr $ffee
    lda #1
    jsr $ffee
    ldx #7
    cursor_loop:
        lda #0
        jsr $ffee
        dex
        bpl cursor_loop

    jsr set_hidden_palette

    ; Define ENVELOPEs.
    lda #0
    sta $70
    define_envelopes_loop:

        ldx $70
        lda envelopes_high,x
        tay
        lda envelopes_low,x
        tax
        lda #8
        jsr $fff1

        inc $70
        lda $70
        cmp #4
        bne define_envelopes_loop

    ldx #0
    init_load_window_loop:

        lda init_load_window_vdu_bytes,x
        jsr $ffee
        inx
        cpx #5
        bne init_load_window_loop

    lda #255
    ldx #<title_block
    ldy #>title_block
    jsr $ffdd

    lda #255
    ldx #<sprites_block
    ldy #>sprites_block
    jsr $ffdd

    lda #255
    ldx #<chars_block
    ldy #>chars_block
    jsr $ffdd

    lda #255
    ldx #<code_block
    ldy #>code_block
    jsr $ffdd

    jmp $1f00

sprites_file_name: .byte "SPRITES", 13
chars_file_name: .byte "CHARS", 13
title_file_name: .byte "TITLE", 13
code_file_name: .byte "CODE", 13

sprites_block: .byte <sprites_file_name, >sprites_file_name
               .byte 0, $54, 0, 0
               .byte 0, $54, 0, 0
               .byte $60, $03, 0, 0
               .byte $60, $57, 0, 0

chars_block: .byte <chars_file_name, >chars_file_name
               .byte $00, $3f, 0, 0
               .byte $00, $3f, 0, 0
               .byte $80, $12, 0, 0
               .byte $80, $51, 0, 0

title_block: .byte <title_file_name, >title_file_name
               .byte $80, $5a, 0, 0
               .byte $80, $5a, 0, 0
               .byte $80, $16, 0, 0
               .byte $00, $71, 0, 0

code_block: .byte <code_file_name, >code_file_name
               .byte $00, $1f, 0, 0
               .byte $00, $1f, 0, 0
               .byte $4b, $1f, 0, 0
               .byte $4b, $3e, 0, 0

init_load_window_vdu_bytes: .byte 28,0,29,19,24

set_hidden_palette:

    lda #1
    sta $70
    lda #0
    sta $71
    jsr set_palette

    ; Run on into the next routine.

set_core_palette:

    lda #2
    sta $70
    lda #2
    sta $71
    jsr set_palette

    lda #3
    sta $70
    lda #3
    sta $71
    jsr set_palette

    rts

set_palette:
                    ; $70=logical colour
                    ; $71=physical colour
    lda $70
    sta $3dfb
    lda $71
    sta $3dfc
    lda #0
    sta $3dfd
    sta $3dfe
    sta $3dff

    lda #$c         
    ldx #$fb
    ldy #$3d
    jsr $fff1
    rts

envelopes_low:  .byte <explosion_envelope, <damage_envelope, <item_envelope, <key_envelope
envelopes_high: .byte >explosion_envelope, >damage_envelope, >item_envelope, >key_envelope

explosion_envelope: .byte 1,1,252,0,0,10,0,0,126,0,0,130,126,126
damage_envelope:    .byte 2,4,8,0,248,2,0,2,126,0,0,130,126,126
item_envelope:      .byte 3,2,8,4,2,10,10,10,126,0,0,130,126,126
key_envelope:       .byte 4,2,4,40,0,8,1,3,126,0,0,130,126,126
